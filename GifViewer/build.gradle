apply plugin: 'android-sdk-manager'
apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 2
        versionName "alpha 0.02"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {

        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            debuggable false
            zipAlign true
            signingConfig signingConfigs.release
        }
    }

    // Read our private signing details from the local properties file
    if (project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyAlias') &&
            project.hasProperty('keyPassword')) {
        println 'gradle.properties OK'
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        println 'gradle.properties not well formed'
        if (!project.hasProperty('storeFile')) println 'storeFile not defined'
        if (!project.hasProperty('storePassword')) println 'storePassword not defined'
        if (!project.hasProperty('keyAlias')) println 'keyAlias not defined'
        if (!project.hasProperty('keyPassword')) println 'keyPassword not defined'

        buildTypes.release.signingConfig = null
    }
}

dependencies {
    compile 'com.android.support:support-v13:19.+'
    compile 'com.jakewharton:butterknife:4.0.1'
    compile 'de.keyboardsurfer.android.widget:crouton:1.8.2'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.0.+'
    compile 'com.github.chrisbanes.photoview:library:1.2.2'

    compile fileTree(dir: 'libs', include: ['*.jar'])
}
